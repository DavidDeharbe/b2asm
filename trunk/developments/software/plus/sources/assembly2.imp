/*************************************************************************
Copyright (c) 2007, David Deharbe

All rights reserved.

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:
    * Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
    * Neither the name of the Universidade Federal do Rio Grande do Norte nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
"AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR
CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*************************************************************************/

IMPLEMENTATION
   assembly2
REFINES addition
SEES programvariant
IMPORTS
  uc.microcontroller
INVARIANT
  a = uc.memory_data(0) & b = uc.memory_data(1) & 
  s = uc.memory_data(2)
INITIALISATION
  BEGIN
    uc.init_data(0, 0);
    uc.init_data(1, 0);
    uc.init_data(2, 0);
    uc.set_end(0)
  END
OPERATIONS
  run =
  VAR pc, end, loopvar IN
    pc := 0;
    end := 8;
    uc.init(pc,end);
    loopvar := 0;
    WHILE pc < end DO
    BEGIN
      CASE pc OF
        EITHER 0 THEN uc.move(0,2)
        OR 1 THEN uc.set_data(3,0)
        OR 2 THEN uc.testeq(1,3)
        OR 3 THEN uc.goto(7)
        OR 4 THEN uc.inc(2)
        OR 5 THEN uc.inc(3)
        OR 6 THEN 
          BEGIN
             uc.goto(2);
             loopvar := loopvar + 1
          END
        OR 7 THEN uc.nop
        END
      END;
      pc <-- uc.get_pc
    END
    INVARIANT 
      pc >= 0 & pc <= end &
      pc = uc.pc & end = uc.end & end = 8 &
      loopvar : NATURAL & 
      uc.memory_data : (NATURAL --> NATURAL) &
      uc.memory_data(0) = a & uc.memory_data(1) = b &
      (pc = 0 => loopvar = 0 & uc.memory_data(2) = s) &
      (pc = 1 => loopvar = 0 & uc.memory_data(2) = a) &
      (pc = 2 => (uc.memory_data(2) = a + uc.memory_data(3) & uc.memory_data(3) = loopvar & loopvar <= b)) &
      (pc = 3 => (uc.memory_data(2) = a + uc.memory_data(3) & uc.memory_data(3) = loopvar & loopvar = b)) &
      (pc = 4 => (uc.memory_data(2) = a + uc.memory_data(3) & uc.memory_data(3) = loopvar & loopvar < b)) &
      (pc = 5 => (uc.memory_data(2) = a + uc.memory_data(3)+1 & uc.memory_data(3) = loopvar & loopvar < b)) &
      (pc = 6 => (uc.memory_data(2) = a + uc.memory_data(3) & uc.memory_data(3) = loopvar + 1 & loopvar < b)) &
      (pc = 7 => (uc.memory_data(2) = a + uc.memory_data(3) & uc.memory_data(3) = loopvar & loopvar = b)) &
      (pc = 8 => (uc.memory_data(2) = a + uc.memory_data(3) & uc.memory_data(3) = loopvar & loopvar = b))
    VARIANT pgvar(pc, uc.memory_data(1), loopvar)
    END
  END
END
