/*************************************************************************
Copyright (c) 2007, David Deharbe

All rights reserved.

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:
    * Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
    * Neither the name of the Universidade Federal do Rio Grande do Norte nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
"AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR
CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*************************************************************************/

IMPLEMENTATION
   assembly2
REFINES max2
IMPORTS
  uc.microcontroller
INVARIANT
  a = uc.memory_data(0) & b = uc.memory_data(1) & m = uc.memory_data(2)
INITIALISATION
  BEGIN
    uc.init_data(0, 0);
    uc.init_data(1, 0);
    uc.init_data(2, 0);
    uc.set_end(0)
  END
OPERATIONS
  run(v) =
  VAR pc, end IN
    pc := 0;
    end := 10;
    uc.init(pc,end);
    WHILE pc < end DO
    BEGIN
      CASE pc OF
        EITHER 0 THEN uc.set_data(3,v)
        OR 1 THEN uc.move(0,1)
        OR 2 THEN uc.move(3,0)
        OR 3 THEN uc.testgt(0,1)
        OR 4 THEN uc.goto(6)
        OR 5 THEN uc.goto(8)
        OR 6 THEN uc.move(0,2)
        OR 7 THEN uc.goto(9)
        OR 8 THEN uc.move(1,2)
        OR 9 THEN uc.nop
        END
      END;
      pc <-- uc.get_pc
    END
    INVARIANT 
      pc >= 0 & pc <= end &
      pc = uc.pc &
      end = uc.end &
      (pc = 0 => (uc.memory_data(0) = a & uc.memory_data(1) = b & uc.memory_data(2) = m)) &
      (pc = 1 => (uc.memory_data(0) = a & uc.memory_data(1) = b & uc.memory_data(2) = m & uc.memory_data(3) = v)) &
      (pc = 2 => (uc.memory_data(0) = a & uc.memory_data(1) = a & uc.memory_data(2) = m & uc.memory_data(3) = v)) &
      (pc = 3 => (uc.memory_data(0) = v & uc.memory_data(1) = a & uc.memory_data(2) = m & uc.memory_data(3) = v)) &
      (pc = 4 => (uc.memory_data(0) = v & uc.memory_data(1) = a & uc.memory_data(2) = m & uc.memory_data(3) = v & v > a)) &
      (pc = 5 => (uc.memory_data(0) = v & uc.memory_data(1) = a & uc.memory_data(2) = m & uc.memory_data(3) = v & v <= a)) &
      (pc = 6 => (uc.memory_data(0) = v & uc.memory_data(1) = a & uc.memory_data(2) = m & uc.memory_data(3) = v & v > a)) &
      (pc = 7 => (uc.memory_data(0) = v & uc.memory_data(1) = a & uc.memory_data(2) = v & uc.memory_data(3) = v & v > a)) &
      (pc = 8 => (uc.memory_data(0) = v & uc.memory_data(1) = a & uc.memory_data(2) = m & uc.memory_data(3) = v & v <= a)) &
      (pc = 9 => (uc.memory_data(0) = v & uc.memory_data(1) = a & uc.memory_data(2) = max({a,v})) & uc.memory_data(3) = v) &
      (pc = 10 => (uc.memory_data(0) = v & uc.memory_data(1) = a & uc.memory_data(2) = max({a,v})) & uc.memory_data(3) = v)
    VARIANT (end - pc)
    END
  END
END
