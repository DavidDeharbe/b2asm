/*************************************************************************
Copyright (c) 2008 David Deharbe, Valerio Medeiros Jr

All rights reserved.

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:
    * Redistributions of source code must retain the above copyright notice, this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright notice, this list of conditions and the following disclaimer in the documentation and/or other materials provided with the distribution.
    * Neither the name of the Universidade Federal do Rio Grande do Norte nor the names of its contributors may be used to endorse or promote products derived from this software without specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
"AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR
CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*************************************************************************/
IMPLEMENTATION
   traffic_light_asm_pic
SEES
   definitions, data_refinement, TYPES

REFINES 
   traffic_light_data_refinement

IMPORTS
   m.PIC

INVARIANT
   m.mem(0) : 0..2 & m.mem(0) = counter

INITIALISATION 
   BEGIN
      m.MOVLW(0);
      m.MOVWF(0)
   END

OPERATIONS
        
advance =
   BEGIN 
      m.CALL(0);
      WHILE m.pc < 10 DO
      BEGIN
         CASE m.pc OF
            EITHER 0  THEN m.BTFSC(0,1)
               OR 1  THEN m.GOTO(8)
               OR 2  THEN m.BTFSC(0,0)
               OR 3  THEN m.GOTO(6)
               OR 4  THEN m.MOVLW(1)
               OR 5  THEN m.GOTO(9)
               OR 6  THEN m.MOVLW(2)
               OR 7  THEN m.GOTO(9)
               OR 8  THEN m.MOVLW(0)
               OR 9  THEN m.MOVWF(0)
            END
         END
      END
      INVARIANT 
         0 <= m.pc & m.pc <= 10 & m.sp > 0 & 
         m.mem(0) : 0..2 &
         ((m.pc = 0) => (m.mem(0) :  0..2 & m.mem(0) = counter)) &
         ((m.pc = 1) => m.mem(0) = 2 & counter = 2) &
         ((m.pc = 2) => m.mem(0) /= 2 & m.mem(0) = counter) &
         ((m.pc = 3) => m.mem(0) = 1 & counter = 1) &
         ((m.pc = 4) => m.mem(0) = 0 & m.mem(0) = counter) &
         ((m.pc = 5) => m.mem(0) = 0 & m.mem(0) = counter & m.w = 1) &
         ((m.pc = 6) => m.mem(0) = 1 & m.mem(0) = counter) &
         ((m.pc = 7) => m.mem(0) = 1 & m.mem(0) = counter & m.w = 2) &
         ((m.pc = 8) => m.mem(0) = 2 & m.mem(0) = counter) &
         ((m.pc = 9) => (m.mem(0) : 0..2 & m.mem(0) = counter & m.w = color_step(counter))) &
         ((m.pc = 10) => (m.mem(0) :  0..2 & m.mem(0) = color_step(counter)))
      VARIANT (10 - m.pc)
      END;
      m.RETURN
   END

END


